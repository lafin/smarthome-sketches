#include <SoftwareSerial.h>
#include "SSD1306Wire.h"

const byte s8_co2[8] = {0xfe, 0x04, 0x00, 0x03, 0x00, 0x01, 0xd5, 0xc5};
const byte s8_fwver[8] = {0xfe, 0x04, 0x00, 0x1c, 0x00, 0x01, 0xe4, 0x03};
const byte s8_id_hi[8] = {0xfe, 0x04, 0x00, 0x1d, 0x00, 0x01, 0xb5, 0xc3};
const byte s8_id_lo[8] = {0xfe, 0x04, 0x00, 0x1e, 0x00, 0x01, 0x45, 0xc3};

const byte s8_clear_reg[8] = {0xfe, 0x06, 0x00, 0x00, 0x00, 0x00, 0x9d, 0xc5};
const byte s8_zero[8] = {0xfe, 0x06, 0x00, 0x01, 0x7c, 0x06, 0x6c, 0xc7};
const byte s8_read_reg[8] = {0xfe, 0x03, 0x00, 0x00, 0x00, 0x01, 0x90, 0x05};
const byte s8_disable_abc[8] = {0xfe, 0x06, 0x00, 0x1f, 0x00, 0x00, 0xac, 0x03};

SoftwareSerial co2Serial(5, 4, false, 256);
SSD1306Wire  display(0x3c, 0, 14);

byte buf[10];

uint16_t calc_crc(byte* buf, uint16_t len) {
  uint16_t crc = 0xFFFF;

  for (uint16_t pos = 0; pos < len; pos++) {
    crc ^= (uint16_t)buf[pos];

    for (uint16_t i = 8; i != 0; i--) {
      if ((crc & 0x0001) != 0) {
        crc >>= 1;
        crc ^= 0xA001;
      } else {
        crc >>= 1;
      }
    }
  }

  return crc;
}

void check_crc() {
  uint16_t crc = calc_crc(buf, 5);
  uint16_t got = (uint16_t)buf[5] + (uint16_t)buf[6] * 256;
  if (crc != got) {
    Serial.print("Invalid checksum.");
    return;
  }
}

void read() {
  while (co2Serial.available() > 0)
  {
    co2Serial.read();
  }
  memset(buf, 0, sizeof(buf));
  co2Serial.readBytes(buf, sizeof(buf));
  check_crc();
  delay(100);
}

void reset_zero() {
  co2Serial.write(s8_disable_abc, 8);
  read();
  delay(100);
  co2Serial.write(s8_clear_reg, 8);
  read();
  delay(100);
  co2Serial.write(s8_zero, 8);
  read();
  delay(2000);
  co2Serial.write(s8_read_reg, 8);
  read();
}

uint16_t readco2() {
  co2Serial.write(s8_co2, 8);
  read();

  return (uint16_t)buf[3] * 256 + (uint16_t)buf[4];
}

void setup() {
  Serial.begin(115200);
  co2Serial.begin(9600);
  delay(10);

  display.init();
  display.flipScreenVertically();
  display.setTextAlignment(TEXT_ALIGN_LEFT);
  display.setFont(ArialMT_Plain_24);

  Serial.println();
  Serial.print("Sensor ID: ");
  co2Serial.write(s8_id_hi, 8);
  read();
  Serial.printf("%02x%02x", buf[3], buf[4]);
  co2Serial.write(s8_id_lo, 8);
  read();
  Serial.printf("%02x%02x", buf[3], buf[4]);
  Serial.println("");

  co2Serial.write(s8_fwver, 8);
  read();
  Serial.printf("Firmware: %d.%d", buf[3], buf[4]);
  Serial.println();
}

void loop() {
  uint16_t co2 = readco2();
  if (co2 < 400) {
    reset_zero();
  }

  Serial.print("CO2: ");
  Serial.println(co2);

  display.clear();
  display.drawString(0, 26, String(co2));
  display.display();

  delay(5000);
}
